import org.ajoberstar.grgit.Grgit

group 'tech.macil.minecraft.rcon'

apply plugin: 'java'
apply plugin: 'net.researchgate.release'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
}

dependencies {
    compile 'org.bukkit:bukkit:1.12.2-R0.1-SNAPSHOT'
    compile 'org.apache.logging.log4j:log4j-core:2.9.1'
    compile 'org.apache.logging.log4j:log4j-api:2.9.1'
    testCompile 'junit:junit:4.12'
}

buildscript {
    repositories {
        mavenCentral()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'org.ajoberstar:gradle-git:1.6.0'
        classpath 'net.researchgate:gradle-release:2.6.0'
    }
}

if (!project.hasProperty("gitCommitHash")) {
    try {
        def repo = Grgit.open(dir: '.')
        ext.gitCommitHash = repo.head().abbreviatedId
    } catch (Exception e) {
        println "Error getting commit hash: " + e.getMessage()
    }
}
if (!project.hasProperty("gitCommitHash")) {
    ext.gitCommitHash = "no_git_id"
}

ext.internalVersion = version + ";" + gitCommitHash

processResources {
    from (sourceSets.main.resources.srcDirs) {
        expand 'internalVersion': project.internalVersion
        include 'plugin.yml'
    }

    from (sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

checkSnapshotDependencies.enabled = false
